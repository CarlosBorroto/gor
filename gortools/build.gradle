/*
 *  BEGIN_COPYRIGHT
 *
 *  Copyright (C) 2011-2013 deCODE genetics Inc.
 *  Copyright (C) 2013-2019 WuXi NextCode Inc.
 *  All Rights Reserved.
 *
 *  GORpipe is free software: you can redistribute it and/or modify
 *  it under the terms of the AFFERO GNU General Public License as published by
 *  the Free Software Foundation.
 *
 *  GORpipe is distributed "AS-IS" AND WITHOUT ANY WARRANTY OF ANY KIND,
 *  INCLUDING ANY IMPLIED WARRANTY OF MERCHANTABILITY,
 *  NON-INFRINGEMENT, OR FITNESS FOR A PARTICULAR PURPOSE. See
 *  the AFFERO GNU General Public License for the complete license terms.
 *
 *  You should have received a copy of the AFFERO GNU General Public License
 *  along with GORpipe.  If not, see <http://www.gnu.org/licenses/agpl-3.0.html>
 *
 *  END_COPYRIGHT
 */
plugins {
    id 'gor.java-library'
    id 'gor.java-application'
    id 'antlr'
    id 'gor.scala-common'
}

project(':gortools') {

    generateGrammarSource {
        arguments += ["-visitor"]
    }

    sourceSets.main.scala.srcDirs file("${projectDir}/build/generated-src/antlr/main")

    dependencies {
        implementation project(':model')
        implementation project(':base')
        implementation project(':util')

        // scala-parser-combinators must be present at compile time for callers so it's defined as an api dependency
        api "org.scala-lang.modules:scala-parser-combinators_2.12:1.1.2"

        implementation "org.freemarker:freemarker:2.3.19"
        implementation "org.yaml:snakeyaml:1.28"
        implementation "commons-io:commons-io:2.8.0"
        implementation "org.apache.commons:commons-lang3:3.12.0"
        implementation "org.scala-lang:scala-compiler:2.12.13"
        implementation "com.fasterxml.jackson.core:jackson-databind:2.12.3"
        implementation "org.scalanlp:breeze_2.12:1.2"
        implementation "colt:colt:1.2.0"
        implementation "com.github.samtools:htsjdk:2.24.1"
        implementation "org.aeonbits.owner:owner:1.0.12"
        implementation 'ch.qos.logback:logback-classic:1.2.3'
        implementation "io.prometheus:simpleclient:0.10.0"
        implementation 'org.apache.parquet:parquet-common:1.12.0'
        implementation 'org.apache.parquet:parquet-column:1.12.0'
        implementation 'org.apache.parquet:parquet-hadoop:1.12.0'
        implementation ('org.apache.hadoop:hadoop-common:3.3.0')

        constraints {
            implementation('org.eclipse.jetty:jetty-server:9.4.40.v20210413') {
                because 'https://gitlab.com/wuxi-nextcode/wxnc-gor/gor/-/pipelines/290960607/security'
            }
            implementation('org.eclipse.jetty:jetty-util:9.4.40.v20210413')
            implementation('org.eclipse.jetty:jetty-servlet:9.4.40.v20210413')
            implementation('org.eclipse.jetty:jetty-webapp:9.4.40.v20210413')
            implementation('org.eclipse.jetty.websocket:websocket-client:9.4.40.v20210413')
            implementation('io.netty:netty-handler:4.1.46.Final')

            implementation('net.minidev:json-smart:2.4.1')
        }

        runtimeOnly "org.scala-lang:scala-reflect:2.12.13"

        testImplementation project(':test')
        testImplementation project(':util')

        testImplementation ('org.apache.hadoop:hadoop-mapreduce-client-core:3.3.0')
        testImplementation "org.mockito:mockito-core:3.7.7"
        testImplementation "org.scalatest:scalatest_2.12:3.0.8"
        testImplementation "org.scalacheck:scalacheck_2.12:1.15.3"
        testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'


        testRuntimeOnly "org.apache.derby:derby:10.15.2.0"
        testRuntimeOnly "org.apache.derby:derbytools:10.15.2.0"

        antlr("org.antlr:antlr4:4.9.2")
    }

    // Tests

    // Run scala tests. These are not automatically picked up by gradle,
    // so we run them like this.
    task testScala(dependsOn: ['testClasses'], type: JavaExec) {
        systemProperty 'logback.configurationFile', new File("${project.rootDir}/tests/config/logback-test.xml").absolutePath
        main = 'org.scalatest.tools.Runner'
        args = ['-R', 'build/classes/scala/test', '-o']

        classpath = sourceSets.test.runtimeClasspath
    }
}

